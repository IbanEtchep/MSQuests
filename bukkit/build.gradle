plugins {
    id 'java'
    id 'io.github.goooler.shadow'
}

dependencies {
    implementation(project(":core"))
    implementation(project(":database"))

    compileOnly("io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT")
    implementation("com.tcoded:FoliaLib:0.5.1")
    implementation("io.github.revxrsal:lamp.common:4.0.0-rc.12")
    implementation("io.github.revxrsal:lamp.bukkit:4.0.0-rc.12")
    implementation("dev.dejvokep:boosted-yaml:1.3.7")
    implementation("com.fasterxml.jackson.core:jackson-databind:2.20.0")
    implementation("com.fasterxml.jackson.core:jackson-core:2.20.0")
    implementation("com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.20.0")
    implementation("com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.20.0")
    implementation(group: 'com.networknt', name: 'json-schema-validator', version: '1.5.9');

    testImplementation("org.mockbukkit.mockbukkit:mockbukkit-v1.21:4.41.0")
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('paper-plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveFileName = "msquests-bukkit-${version}.jar"

    dependencies {
        include(dependency('com.tcoded:FoliaLib:.*'))
        include(dependency('dev.dejvokep:boosted-yaml:.*'))
        include(dependency('io.github.revxrsal:lamp.*'))
        include(dependency('com.fasterxml.jackson.core:jackson-databind:.*'))
        include(dependency('com.fasterxml.jackson.core:jackson-core:.*'))
        include(dependency('com.fasterxml.jackson.core:jackson-annotations:.*'))
        include(dependency('com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:.*'))
        include(dependency('com.fasterxml.jackson.datatype:jackson-datatype-jsr310:.*'))
        include(dependency('com.networknt:json-schema-validator:.*'))
        include(project(':core'))
        include(project(':database'))
    }

    relocate "com.tcoded.folialib", "com.github.ibanetchep.msquests.lib.folialib"
    relocate "dev.dejvokep.boostedyaml", "com.github.ibanetchep.msquests.lib.boostedyaml"
    relocate "revxrsal.commands", "com.github.ibanetchep.msquests.lib.commands"
    relocate "com.fasterxml.jackson", "com.github.ibanetchep.msquests.lib.jackson"
    relocate "com.networknt", "com.github.ibanetchep.msquests.lib.networknt"

    archiveClassifier.set('')
}

tasks.register('copyPlugin', Copy) {
    from shadowJar.archiveFile
    into "../docker/plugins"

    from('src/main/resources/config.yml') {
        into "./MSQuests"
    }
}

build {
    dependsOn shadowJar
    finalizedBy(tasks.named("copyPlugin"))
}